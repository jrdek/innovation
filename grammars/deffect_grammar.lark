%import .common (BASE_COLOR, ICON, NUM, CARD_NAME)

%ignore /\s+/
%ignore "#" /[^\n]/*
%ignore ","

cards: card+

card: CARD_NAME "(" age BASE_COLOR ")" "{" icons dogma "}"

?icons: ICON~1..6

dogma: dogma_effect+

age: "[" value "]"

dogma_effect: effect_header stmts

effect_header: EFFECT_CLASS "(" ICON ")"

EFFECT_CLASS: "Shared" | "Demand"

// ARGS TO STATEMENTS

DIRECTION: "left" | "right" | "up"
HAND: "hand" "s"?
SCORE_PILE: "score pile" "s"?
ACHIEVEMENTS: "achievements"
BOARD: "board" "s"?
TOP_CARDS: "top cards"

general_zone: HAND
    | SCORE_PILE
    | ACHIEVEMENTS
    | BOARD
    | TOP_CARDS
    | BASE_COLOR "pile"

MY: "my"
YOUR: "your"
THEIR: "their"
EVERYONES: "everyone's"
EVERYONE_ELSES: "everyone else's"
ANYONES: "anyone's"
ANYONE_ELSES: "anyone else's"

YOU: "you"
SOMEONE: "some player"

player: YOU
    | "the player with" COUNT_SUPERLATIVE countable

specific_players: SOMEONE ("s.t. (" condition ")")? "'s"

player_adj: MY
    | YOUR
    | THEIR
    | specific_players
    | EVERYONES
    | ANYONES
    | EVERYONE_ELSES
    | ANYONE_ELSES

zone: player_adj general_zone 

VAL_SUPERLATIVE: "highest" | "lowest"
COUNT_SUPERLATIVE: "strictly most" | "strictly fewest"

card_sel_lambda: sel_num_cards | sel_all_cards | NUM

sel_num_cards: "the" VAL_SUPERLATIVE? value?
sel_all_cards: "all the" VAL_SUPERLATIVE? | "all" VAL_SUPERLATIVE?


// TODO: "its feature"...
ITS_COLOR: "its color"

QUANTIFIER: "any of" | "all of"  // TODO: redundant?

card_is_like: QUANTIFIER? specific_cards ("is"|"are") color_select 
    | QUANTIFIER? specific_cards ("has"|"have") "a" ICON
    | CARD_NAME "is" PREPOS zone

CHOSEN: "chosen"

?color_select: ITS_COLOR
    | BASE_COLOR
    | "(" color_select ")"

ACTOR: "you" | "anyone" | "nobody"

cards_transferred: ACTOR "transferred" ANY_AMT CARDS
ANY_AMT: "no" | "any"

ONLY_YOU_HAVE_ALL_COLORS: "only-you-have-all-colors"

not_cond: "not" "(" condition ")"

AT_LEAST: "at least"
BELOW: "below"

SCORE: "score"
POINTS: "points"

DEMOCRACY_RECORD: "the democracy record"

COUNT: "#"

ANY_NUMBER_OF: "any number of"

ROUND: "rounded up" | "rounded down"

?value: NUM
    | feature_of_card
    | ANY_NUMBER_OF
    | DEMOCRACY_RECORD
    | player_adj SCORE
    | CHOSEN
    | COUNT countable
    | value "plus" NUM
    | value "times" NUM
    | value "divided by" NUM ROUND?
    | VAL_SUPERLATIVE "value" PREPOS zone
    | "(" value ")"  // sometimes it's nice to have parens for readability

compare_op: AT_LEAST | BELOW

comparison: player_adj? value "is" compare_op? value

ANY_COLOR: "any one color"

ANY_ONE: "any one"

player_color_cards: player_adj color_select CARDS
    | player_adj CARDS "of" feature_of_card
    | ANY_COLOR "of" player_adj CARDS ("which is splayed" DIRECTION)?
    | "(" player_color_cards ")"

pile_is_splayed: player_color_cards "are splayed" DIRECTION?

cond_or_cond: condition "or" condition

cond_and_cond: condition "and" condition

SAME: "the same" | "its"

a_certain_feature: SAME FEATURE | ANY_ONE FEATURE

value_is_superlative: value "is" COUNT_SUPERLATIVE

?condition: cards_transferred 
    | ONLY_YOU_HAVE_ALL_COLORS
    | pile_is_splayed
    | feature_already_seen
    | card_is_like
    | comparison
    | value_is_superlative
    | not_cond
    | cond_or_cond
    | cond_and_cond
    | "(" condition ")"

IT: "it" 
THEM: "them"

CARDS: /cards?/

distinct_features: "distinct" FEATURE "s" PREPOS (THEM | zone)

feature_already_seen: FEATURE "already seen"

FEATURE: "value" | "color"

feature_of_card: "its" FEATURE 
    | "the"? FEATURE "of" specific_cards
    | specific_cards "'s" FEATURE

countable: referent
    | POINTS
    | kind_of_card PREPOS zone 
    | ICON PREPOS zone
    | ICON PREPOS specific_cards
    | distinct_features
    | "(" countable ")"

card_of_certain_age: age 
card_of_certain_color: BASE_COLOR kind_of_card
card_not_of_certain_color: "non-" BASE_COLOR kind_of_card
card_with_certain_icon: kind_of_card "with" "a" ICON
card_without_certain_icon: kind_of_card "without" "a" ICON
card_of_certain_feature: kind_of_card "of" a_certain_feature

kind_of_card: PILE_LOC kind_of_card
    | card_of_certain_age
    | card_of_certain_color
    | card_not_of_certain_color
    | card_with_certain_icon
    | card_without_certain_icon
    | card_of_certain_feature
    | CARDS

THE_OTHER_ONE: "the other one"

referent: IT | THEM | CHOSEN | THE_OTHER_ONE

PILE_LOC: "top" | "bottom"

PREPOS: "from" | "in" | "on" | "among"

range: value | up_to_value

up_to_value: "up to" value

MY_CHOICE: "my choice"

?specific_cards: referent
    | MY_CHOICE
    | card_sel_lambda kind_of_card ("other than" CARD_NAME)? ("s.t." condition)? PREPOS zone
    | card_sel_lambda kind_of_card "of" a_certain_feature PREPOS zone
    | range kind_of_card PREPOS zone
    | player_adj PILE_LOC BASE_COLOR CARDS
    | player_adj PILE_LOC CARDS "of" a_certain_feature  // this is too permissive FIXME
    | card_sel_lambda PILE_LOC kind_of_card


stmts: stmt
    | "{" (stmt "\n")+ "}"


// KINDS OF STATEMENTS

stmt: draw_stmt
    | return_stmt
    | transfer_stmt
    | meld_stmt
    | if_stmt
    | you_may_stmt
    | reveal_stmt
    | repeat_stmt
    | score_stmt
    | tuck_stmt
    | exchange_stmt
    | splay_stmt
    | special_achieve_stmt
    | democracy_stmt
    | rearrange_stmt
    | win_stmt
    | choose_features_stmt
    | choose_card_stmt
    | nuke_stmt
    | end_dogma_action_stmt
    | dogma_combo_stmt


draw_stmt: "draw" range age

return_stmt: "return" specific_cards

transfer_stmt: "transfer" specific_cards "to" zone

meld_stmt: "meld" specific_cards

splay_stmt: "splay" player_color_cards DIRECTION
    | "unsplay" player_color_cards

score_stmt: "score" specific_cards

reveal_stmt: "reveal" specific_cards

if_stmt: "if" condition stmts ("else" stmts)?

you_may_stmt: "you may" stmts ("or" stmts)?

repeat_stmt: "repeat this effect"

tuck_stmt: "tuck" specific_cards

exchange_stmt: "exchange" specific_cards "with" specific_cards

rearrange_stmt: "rearrange" player_color_cards

democracy_stmt: "set the democracy record"

special_achieve_stmt: "claim the" CARD_NAME "special achievement"

win_stmt: player /wins?/

choose_features_stmt: "choose" value FEATURE "s"?

choose_card_stmt: "choose" specific_cards

nuke_stmt: "nuke" specific_cards

end_dogma_action_stmt: "end the dogma action"

dogma_combo_stmt: "dogma combo" 